      *****************************************************************
      *
      * Licensed Materials - Property of IBM
      *
      * Restricted Materials of IBM
      *
      * 5655-CE3
      *
      * (C) Copyright IBM Corp. 2017
      *
      * US Government Users Restricted Rights - Use, duplication or
      * disclosure restricted by GSA ADP Schedule Contract with
      * IBM Corp.
      *****************************************************************
      *****************************************************************
      *                                                               *
      * NAME = BAQCAPPO                                               *
      *                                                               *
      * DESCRIPTIVE NAME = COBOL sample program to call RESTful       *
      *                    API using z/OS Connect EE API requester    *
      *****************************************************************
       IDENTIFICATION DIVISION.
       PROGRAM-ID. WATSON.
       DATA DIVISION.
       WORKING-STORAGE SECTION.

      * Copy API requester required copybook
       COPY BAQRINFO.

      *----------------------------------------------------------------*
      * API Request and Response, need to be replaced for each operation
      *----------------------------------------------------------------*
      * Copy request and response copybooks. These copybooks are
      * generated by build toolkit and used to describe the structure
      * of request and response.
      * These declarations are required.
      *
      * The request has eight fields to describe the model. The
      * source language, target language and text are needed for
      * the translation program.
      *
      *   06 model-id-num                  PIC S9(9) COMP-5 SYNC.
      *
      *   06 model-id.
      *       09 model-id2-length          PIC S9999 COMP-5 SYNC.
      *       09 model-id2                 PIC X(255).
      *
      *   06 Xsource-num                   PIC S9(9) COMP-5 SYNC.
      *
      *   06 Xsource.
      *       09 Xsource2-length           PIC S9999 COMP-5 SYNC.
      *       09 Xsource2                  PIC X(255).
      *
      *    06 target-num                   PIC S9(9) COMP-5 SYNC.
      *
      *    06 target.
      *       09 target2-length            PIC S9999 COMP-5 SYNC.
      *       09 target2                   PIC X(255).
      *
      *    06 Xtext-num                    PIC S9(9) COMP-5 SYNC.
      *
      *    06 Xtext OCCURS 255.
      *       09 Xtext2-length             PIC S9999 COMP-5 SYNC.
      *       09 Xtext2                    PIC X(255).
      *       09 filler                    PIC X(1).
      *
      * The response has four fields. The translated text, and the
      * number of words and number of characters in the translated text.
      *    06 word-count                   PIC S9(18) COMP-5 SYNC.
      *    06 character-count              PIC S9(18) COMP-5 SYNC.
      *
      *    06 translations2-num            PIC S9(9) COMP-5 SYNC.
      *
      *    06 translations OCCURS 255.
      *        09 translation-length       PIC S9999 COMP-5 SYNC.
      *        09 translation              PIC X(255).
      *        09 filler                   PIC X(1).
      *
       01  REQUEST.
           COPY API00Q01.
       01  RESPONSE.
           COPY API00P01.
       01  API-INFO-API1.
           COPY API00I01.

      * Request and Response data structures
       01 BAQ-REQUEST-PTR             USAGE POINTER.
       01 BAQ-REQUEST-LEN             PIC S9(9) COMP-5 SYNC.
       01 BAQ-RESPONSE-PTR            USAGE POINTER.
       01 BAQ-RESPONSE-LEN            PIC S9(9) COMP-5 SYNC.
       77 COMM-STUB-PGM-NAME             PIC X(8) VALUE 'BAQCSTUB'.

      * Counter
       01 WF-LOOP-COUNTER          PIC S9(08) COMP.

       LINKAGE SECTION.

      *----------------------------------------------------------------*
      * Procedures
      *----------------------------------------------------------------*
       PROCEDURE DIVISION.
       MAINLINE SECTION.

           INITIALIZE REQUEST.
           INITIALIZE RESPONSE.
           INITIALIZE WF-LOOP-COUNTER.

      * Use pointer and length to specify the location of
      * the request and response data structures.
      * These assignments are required.
           SET BAQ-REQUEST-PTR TO ADDRESS OF REQUEST.
           MOVE LENGTH OF REQUEST TO BAQ-REQUEST-LEN.
           SET BAQ-RESPONSE-PTR TO ADDRESS OF RESPONSE.
           MOVE LENGTH OF RESPONSE TO BAQ-RESPONSE-LEN.

      * Model is not used in this sample. 0 is assigned to
      * model-id-num.
           MOVE 0 TO model-id-num.
      * Xsource is source language, which is 'English' in this sample.
           MOVE 1 TO Xsource-num.
           MOVE 'EN' TO Xsource2.
           MOVE 2 TO Xsource2-length.
      * target is target language, which is 'French' in this sample.
           MOVE 1 TO target-num.
           MOVE 'FR' TO target2.
           MOVE 2 TO target2-length.
      * Xtext is the text to be translated, which is 'How are you'
      * in this sample.
           MOVE 1 TO Xtext-num.
           MOVE 'How are you' TO Xtext2(1).
           MOVE 11 TO Xtext2-length(1).

      * Call BAQCSTUB, pass parameters, and receive result.
      * This call is required.
           CALL COMM-STUB-PGM-NAME USING
                           BY REFERENCE   API-INFO-API1
                           BY REFERENCE   BAQ-REQUEST-INFO
                           BY REFERENCE   BAQ-REQUEST-PTR
                           BY REFERENCE   BAQ-REQUEST-LEN
                           BY REFERENCE   BAQ-RESPONSE-INFO
                           BY REFERENCE   BAQ-RESPONSE-PTR
                           BY REFERENCE   BAQ-RESPONSE-LEN.

      * The BAQ-RETURN-CODE field in 'BAQRINFO' indicates whether this
      * API call is successful.

      * When BAQ-RETURN-CODE is 'BAQ-SUCCESS', response is
      * successfully returned and fields in RESPONSE copybook
      * can be obtained. Display the translation result.
           IF BAQ-SUCCESS THEN
              DISPLAY "WORD NUMBER: " word-count
              DISPLAY "CHARACTER NUMBER: " character-count
              DISPLAY "RECORD NUMBER: " translations2-num
              IF translations2-num NOT EQUAL 0 THEN
                 PERFORM
                 VARYING  WF-LOOP-COUNTER FROM 1 BY 1
                 UNTIL WF-LOOP-COUNTER > translations2-num
                 DISPLAY "TRANSLATIONS: " translation(WF-LOOP-COUNTER)
                 DISPLAY "TRANSALATION LENGTH: "
                               translation-length(WF-LOOP-COUNTER)
                 END-PERFORM
              END-IF

      * Otherwise, an error occurred in the API, z/OS Connect EE server
      * or communication stub. 'BAQ-STATUS-CODE' and
      * 'BAQ-STATUS-MESSAGE' contains the error information.
           ELSE
              EVALUATE TRUE
      * When error occurs in API, BAQ-RETURN-CODE is BAQ-ERROR-IN-API.
      * BAQ-STATUS-CODE is the HTTP response code of API.
                 WHEN BAQ-ERROR-IN-API
                   DISPLAY "API RETURN ERROR: " BAQ-STATUS-CODE
                   DISPLAY "API RETURN ERROR MESSAGE: "
                                             BAQ-STATUS-MESSAGE
      * When error occurs in server, BAQ-RETURN-CODE is
      * BAQ-ERROR-IN-ZCEE
      * BAQ-STATUS-CODE is the HTTP response code of
      * z/OS Connect EE server.
                 WHEN BAQ-ERROR-IN-ZCEE
                   DISPLAY "SERVER RETURN ERROR: " BAQ-STATUS-CODE
                   DISPLAY "SERVER RETURN ERROR MESSAGE: "
                                            BAQ-STATUS-MESSAGE
      * When error occurs in communication stub, BAQ-RETURN-CODE is
      * BAQ-ERROR-IN-STUB, BAQ-STATUS-CODE is the error code of STUB.
                 WHEN BAQ-ERROR-IN-STUB
                   DISPLAY "STUB RETURN ERROR: " BAQ-STATUS-CODE
                   DISPLAY "STUB RETURN ERROR MESSAGE: "
                                            BAQ-STATUS-MESSAGE
              END-EVALUATE
           END-IF.

           STOP RUN.
       END PROGRAM WATSON.
